apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
}

// --- DEPENDENCIES ---

dependencies {

    // ============= COMPILATION =============

    testImplementation 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'

    // ============= MONGO TYPES =============
    
	// https://mvnrepository.com/artifact/org.mongodb/bson
	implementation group: 'org.mongodb', name: 'bson', version: '3.12.1'

}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

// version = '1.0.14-SNAPSHOT'
version = '1.0.14'

modifyPom {
    project {
        artifactId 'datatree-core'
        name 'DataTree Core API'
        description 'Extensible Java Library for reading, manipulating and writing hierarchical data structures from/to various formats.'
        url 'https://berkesa.github.io/datatree/'
        inceptionYear '2017'

        scm {
            url 'https://berkesa.github.io/datatree/'
            connection 'scm:https://github.com/berkesa/datatree.git'
            developerConnection 'scm:git://github.com/berkesa/datatree.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        
        dependencies {
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}
